<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009"
					   xmlns:s="library://ns.adobe.com/flex/spark"
					   xmlns:mx="library://ns.adobe.com/flex/mx"
					   xmlns:ui="com.complib.common.ui.*"
					   minWidth="800" minHeight="600" closing="onClosing(event)"
					   creationComplete="initApp();">
	
	<s:states>
		<s:State name="Start"/>
		<s:State name="Test"/>
		<s:State name="Finish"/>
	</s:states>
	
	<fx:Declarations>
		<s:Sequence id="fadeSignalAcknowledgement" duration="1000" target="{signal_ak}">
			<s:Fade id="fadeShow" alphaFrom="0.0" alphaTo="1.0"/>
			<s:Fade id="fadeHide" alphaFrom="1.0" alphaTo="0.0"/>
		</s:Sequence>
	</fx:Declarations>
	
	
	<fx:Script>
		<![CDATA[
			
			// To get as3xls to work, make sure it is in the /libs folder.
			// In Flash Builder, go to menu Windows > Preferences > General > Workspace > Linked Resources
			// and add the .swc file to the linked resources.
			// the import "import com.as3xls.xls.*;" tend to disappear when you add a new one
			
			/*-----------------*/
			/*---- Imports ----*/
			/*-----------------*/
			
			import com.as3xls.xls.ExcelFile;
			import com.as3xls.xls.Sheet;
			
			import flash.events.Event;
			import flash.events.TimerEvent;
			import flash.utils.Timer;
			
			import mx.formatters.DateFormatter;
			
			/*-------------------*/
			/*---- Constants ----*/
			/*-------------------*/
			
			private const SIGNALS_NUMBER:Number = 10;
			
			private const SECOND:Number = 1000;
			private const MINUTE:Number = 60 * SECOND;
			
			private const TIMER_INTERVAL:Number = 15 * SECOND;
			private const TIMER_FIRST_SIGNAL:Number = 15 * SECOND;
			
			private const MIN_RANDOM_INTERVAL:Number = 4;
			private const MAX_RANDOM_INTERVAL:Number = 15;
			
			private const XML_NAME:String = "stroop_results.xml";
			private const XLS_NAME:String = "stroop_results.xls";
			
			/*--------------------*/
			/*---- Attributes ----*/
			/*--------------------*/
			
			protected var xls:ExcelFile;
			protected var sheet:Sheet;
			
			private var excelStroopRow:int;
			private var excelSignalsRow:int;
			
			private var xmlResult:String;
			
			private var nbErrorsStroop:int;
			private var nbSignalsSent:int;
			
			private var timerLaunchSignals:Timer;
			private var timerSignalDuration:Timer;
			
			private var nativeProcessStartupInfo:NativeProcessStartupInfo;
			private var process:NativeProcess;
			
			private var CurrentDateTime:Date;
			private var CurrentDF:DateFormatter;
			
			private var pathFolder:String;
			
			/*---------------*/
			/*---- Array ----*/
			/*---------------*/
			
			private var signals:Array = 
			[
				"",
				""
			];
			
			/*-----------------*/
			/*---- Methods ----*/
			/*-----------------*/
			
			/*---- Application Methods ----*/
			
			/**
			 * This function is called when the application
			 * is closed and calls xml and xls write methods 
			 */
			private function onClosing(event:Event):void{
				
			}
			
			/**
			 * This function is called when the application 
			 * is launched and initializes variables
			 */
			private function initApp():void {
				this.currentState='Start';
				
				this.nativeProcessStartupInfo = new NativeProcessStartupInfo();
				this.process = new NativeProcess();
				
				this.CurrentDateTime = new Date();
				this.CurrentDF = new DateFormatter();
				this.CurrentDF.formatString = "YYYY/MM/DD HH:NN:SS.QQQ"
				
				this.initPathFolder();
				this.initXML();
				this.initExcel();
				this.initTimers();
				
				this.addEventListener(KeyboardEvent.KEY_UP, keyHandler);
			}
			
			
			/*---- State Change Methods ----*/
			
			/**
			 * This function is called to reach the
			 * state 'start' of this application
			 */
			private function startTest():void {
				this.currentState='Test';
				
				this.nbSignalsSent = 0;
				this.nbErrorsStroop = 0;
				
				this.focusManager.setFocus(color_test);
				
				this.timerLaunchSignals.start();
			}
			
			/**
			 * This function is called to reach the
			 * state 'finish' of this application
			 */
			private function finishTest():void {
				this.currentState='Finish';
				
				this.timerLaunchSignals.stop();
				this.timerSignalDuration.stop();
				
				this.writeXML();
				this.writeExcel();
				
			}
			
			
			/*---- Stroop Methods ----*/
			
			/**
			 * This function handle keyboard event
			 * @event : Keyboard event
			 */
			private function keyHandler(event:KeyboardEvent):void {
				var time:String = CurrentDF.format(CurrentDateTime);
				
				xmlResult += "<signal_detected>"+time+"</signal_detected>"+"\n";
				fadeSignalAcknowledgement.end();
				fadeSignalAcknowledgement.play();
				
				if(timerSignalDuration.running) {
					stopSignal(null);
				}
			}
			
			/**
			 * This function handles an answer to the stroop test.
			 * It registers the results.
			 * @event : Event to register
			 */
			private function printMessage(event:Event):void {
				var time:String = CurrentDF.format(CurrentDateTime);
				
				if(buttonToColorUint(event)==color_test.getStyle('color')){
					message.text = "Correct answer";
					xmlResult += "<correct_answer>"+time+"</correct_answer>"+"\n";
					
					// Excel results
					sheet.setCell(excelStroopRow, 5, time);
					sheet.setCell(excelStroopRow, 6, nbErrorsStroop);
					excelStroopRow++;
					
					// Next question
					nbErrorsStroop = 0;
					sheet.setCell(excelStroopRow, 4, time);
					color_test.setStyle('color',generateQuestionColor());
					color_test.text=generateQuestionText();
				}
				else{
					message.text = "Wrong answer";
					xmlResult += "<wrong_answer>"+time+"</wrong_answer>"+"\n";
					
					nbErrorsStroop++;
				}
			}
			
			/**
			 * This function generate a number between 1 to 4;
			 */
			private function generateNumber1to4():Number {
				var low:Number = 1;
				var high:Number= 4;
				var result:Number = Math.floor(Math.random() * (1 + high - low)) + low;
				return result;
			}
			
			/**
			 * This function generate a question color;
			 */
			private function generateQuestionColor():String {
				var num:Number = generateNumber1to4();
				switch(num){
					case 1:
						// Red
						return "#FF0000";
					case 2:
						// Blue
						return "#0000FF";
					case 3:
						// Green
						return "#169C16";
					case 4:
						// Yellow
						return "#F7FE2E";
					default:
						// White
						return "#FFFFFF";
				}
			}
			
			/**
			 * This function generate a question text;
			 */
			private function generateQuestionText():String {
				var num:Number = generateNumber1to4();
				switch(num){
					case 1:
						// Red
						return "Red";
					case 2:
						// Blue
						return "Blue";
					case 3:
						// Green
						return "Green";
					case 4:
						// Yellow
						return "Yellow";
					default:
						// White
						return "White";
				}
			}
			
			/**
			 * 
			 */
			private function buttonToColorUint(event:Event):String {
				switch(event.target.id){
					case "Red":
						return "16711680";
					case "Blue":
						return "255";
					case "Green":
						return "1481750";
					case "Yellow":
						return "16252462";
					default:
						return "#FFFFFF";
				}
				
			}
				
			
			/*---- Executable Methods ----*/
			
			/**
			 * This method is used to call a .exe application
			 * to communicate with our prototype.
			 * @path : path of the .exe to execute
			 */
			private function runExeFile(path:String):void {
				if(NativeProcess.isSupported){
					var file:File = File.applicationDirectory;
					file = file.resolvePath(path);
					nativeProcessStartupInfo.executable = file;
					process.start(nativeProcessStartupInfo);
				}
			}
			
			
			/*---- XML methods ----*/
			
			/**
			 * This function init the pathFolder variable where
			 * the name and the path of the folder are stocked
			 */
			private function initPathFolder():void {
				var time:String = CurrentDF.format(CurrentDateTime);
				var myPattern:RegExp = /\//g;  
				
				time = time.replace(myPattern, "-");
				myPattern = /\./g;
				time = time.replace(myPattern, "-");
				myPattern = /:/g;
				time = time.replace(myPattern, "_");
				
				this.pathFolder = "./Stroop_Task_" + time + "/";
			}
			
			/**
			 * This function init the XML result
			 */
			private function initXML():void {
				this.xmlResult = "<?xml version=\"1.0\" encoding=\"UTF-8\"?>"+"\n";
			}
			
			/**
			 * This function write into a xml file into the pathFolder
			 */
			private function writeXML():void {
				var file:File = File.desktopDirectory.resolvePath(pathFolder + XML_NAME);
				var stream:FileStream = new FileStream();
				
				stream.open(file, FileMode.WRITE);
				stream.writeUTFBytes(xmlResult);
				stream.close();
			}

			
			/*---- XLS methods ----*/
			
			/**
			 * This method initialize the Excel file for registering data.
			 */
			private function initExcel():void {
				xls = new ExcelFile();
				sheet = new Sheet();
				sheet.resize(160, 12);
				
				sheet.setCell(0, 0, "Today's date:");
				var time:String = CurrentDF.format(CurrentDateTime);
				sheet.setCell(0, 2, time);
				
				sheet.setCell(1, 0, "Signal sent date");
				sheet.setCell(1, 1, "Signal acknowledged date");
				sheet.setCell(1, 2, "Signal id");
				
				sheet.setCell(1, 4, "Stroop test date");
				sheet.setCell(1, 5, "Stroop correct answer date");
				sheet.setCell(1, 6, "Stroop errors");
				
				excelSignalsRow = 2;
				excelStroopRow = 2;
			}
			
			/**
			 * This method writes the Excel file.
			 * Called in the end of the execution.
			 */
			private function writeExcel():void {
				xls.sheets.addItem(sheet);
				
				var bytes:ByteArray = xls.saveToByteArray();
				var stream:FileStream = new FileStream();
				var time:String = CurrentDF.format(CurrentDateTime);
				
				var myPattern:RegExp = /\//g;  
				time = time.replace(myPattern, "-");
				myPattern = /\./g;
				time = time.replace(myPattern, "-");
				myPattern = /:/g;
				time = time.replace(myPattern, "_");
				
				var file:File = File.desktopDirectory.resolvePath(pathFolder + XLS_NAME);
				
				stream.open(file, FileMode.WRITE);
				stream.writeBytes(bytes);
				stream.close();
			}
			
			
			/*---- Timer Methods ----*/
			
			/**
			 * This function initializes the two timers of the application
			 *   - timerLaunchSignals
			 *   - timerSignalDuration
			 */
			private function initTimers(): void {
				timerLaunchSignals = new Timer(TIMER_FIRST_SIGNAL, 1);
				timerSignalDuration = new Timer(5 * SECOND, 1);
				
				timerLaunchSignals.addEventListener(TimerEvent.TIMER, sendSignal);
				timerSignalDuration.addEventListener(TimerEvent.TIMER, stopSignal);
			}
			
			/**
			 * This function send signal to smart cap and
			 * initalizes the timer signal duration
			 */
			private function sendSignal(evt:TimerEvent):void {
				var time:String = CurrentDF.format(CurrentDateTime);
				var couleur:int;
				var forme:int
				var movement:int;
				
				// --------------------------------------------
				// PARTIE A REVOIR AVEC LA CONNECTION DU PROTO
				// --------------------------------------------
				
				// => Le random n'est peut-être pas la meilleur idée,
				// => préférer un tableau statique.
				// => Voir attribut Array
				
				// Call our prototype with randomized parameters 
				couleur = Math.floor(Math.random() *
					(2 - 1 + 1)) + 1;
				forme = Math.floor(Math.random() *
					(3 + 1));
				movement = Math.floor(Math.random() *
					(2 + 1));
				
				// Register the parameters in the Excel file
				sheet.setCell(excelSignalsRow, 2, 100*couleur + 10*forme + movement);
				
				// Call the .exe and register the starting time
				// this.runExeFile("C:\\Windows\\System32\\notepad.exe");
				
				// ---------------------------------------------
				// FIN DE LA PARTIE
				// --------------------------------------------
				
				// Add a line to XML
				xmlResult += "<signal_send>"+time+"</signal_send>"+"\n";
				
				// Add cell date to excel
				sheet.setCell(excelSignalsRow, 0, time);
				
				// Start of the duration signal timer (5sec)
				timerSignalDuration.start();
			}
			
			/**
			 * This function stop the curent signal if the user acknowledge it or
			 * the timer signal duration is finished
			 */
			private function stopSignal(evt:TimerEvent):void {
				var time:String = CurrentDF.format(CurrentDateTime);
				var random:int;
				
				// --------------------------------------------
				// PARTIE A REVOIR AVEC LA CONNECTION DU PROTO
				// --------------------------------------------
				
				// => Mettre fin au signal (faire une fonction)
				
				// --------------------------------------------
				// FIN DE LA PARTIE
				// --------------------------------------------
				
				// Add a line to XML file
				xmlResult += "<signal_stop>"+time+"</signal_stop>"+"\n";
				
				// End is registered either the signal has been seen or not
				sheet.setCell(excelSignalsRow++, 1, time);
				
				// Increments number of signals which is done 
				nbSignalsSent++;
				if(nbSignalsSent >= SIGNALS_NUMBER) {
					finishTest();
					return;
				}
				
				// Reset of the duration signal timer
				timerSignalDuration.stop();
				timerSignalDuration.reset();
				
				// Reset of the timer to lauch signals with a random value 
				random = Math.floor(Math.random() * (MAX_RANDOM_INTERVAL - MIN_RANDOM_INTERVAL + 1)) + MIN_RANDOM_INTERVAL;
				timerLaunchSignals new Timer(random * SECOND, 1);
				timerLaunchSignals.addEventListener(TimerEvent.TIMER, sendSignal);
				timerLaunchSignals.start();
			}

		]]>
	</fx:Script>
	
	<mx:Panel width="100%" height="100%" horizontalAlign="center" layout="horizontal"
			  paddingBottom="10" paddingLeft="10" paddingRight="10" paddingTop="100"
			  title="Stroop Task" verticalAlign="middle">
		
		<mx:VBox>
			<mx:TextArea id="signal_ak" includeIn="Test" visible="true" width="100%" height="200%"
						 alpha="0.0" borderVisible="false" fontSize="28"
						 text="Signal acknowledgement received" textAlign="center"/>
			
			<mx:TextArea id="color_test" includeIn="Test" width="100%" height="100%"
						 borderVisible="false" color="#F7FE2E" contentBackgroundColor="#D8D8D8"
						 editable="false" fontSize="32" text="Blue" textAlign="center"/>
			
			<mx:HBox>
				<mx:Label id="finishLabel" includeIn="Finish" fontSize="28" text="Thank you for your participation." />
				
				<mx:Button id="startButton" includeIn="Start" label="Start the test"
						   click="startTest();" focusEnabled="false"/>
				
				<mx:Button id="Green" includeIn="Test" label="Green" click="printMessage(event)"
						   focusEnabled="false"/>
				
				<mx:Button id="Blue" includeIn="Test" label="Blue" click="printMessage(event)"
						   focusEnabled="false"/>
				
				<mx:Button id="Yellow" includeIn="Test" label="Yellow" click="printMessage(event)"
						   focusEnabled="false"/>
				
				<mx:Button id="Red" includeIn="Test" label="Red" click="printMessage(event)"
						   focusEnabled="false"/>
			</mx:HBox>
			
			<mx:TextArea id="message" includeIn="Test" width="100%" height="100%" color="#0000FF"
						 editable="false" text=""/>
			
		</mx:VBox>
		
	</mx:Panel>
	
</s:WindowedApplication>

